||| Copyright (C) 1998-2009, Sumisho Computer Systems Corp. All Rights Reserved.

||| Licensed under the Apache License, Version 2.0 (the "License");
||| you may not use this file except in compliance with the License.
||| You may obtain a copy of the License at
||| 
|||     http://www.apache.org/licenses/LICENSE-2.0
||| 
||| Unless required by applicable law or agreed to in writing, software
||| distributed under the License is distributed on an "AS IS" BASIS,
||| WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
||| See the License for the specific language governing permissions and
||| limitations under the License.

|||
||| @authors Hitoshi Okada
|||

{doc-next
    {purpose RecordFieldのユーティリティ}
}
{define-class public open RecordFieldUtil

  {doc-next
    {purpose 2つの{docref RecordField}が同一か否かをチェックします。}
    {parameter f1, 対象となる {docref RecordField}。}
    {parameter f2, 対象となる {docref RecordField}。}
    {return-vals 同一の場合true}
  }
  {define-proc public inline {equal?
                                 f1:RecordField,
                                 f2:RecordField
                             }:bool
    {return
        (f1.name == f2.name and
         f1.caption == f2.caption and
         f1.default-value == f2.default-value and
         f1.domain == f2.domain and
         f1.index-type == f2.index-type and
         f1.modifiable? == f2.modifiable? and
         f1.nullable? == f2.nullable? and
         f1.required? == f2.required?)
    }
  }
  
  {doc-next
    {purpose SQL文(CREATE文)のAUTO_INCREASEに近い機能で、連番を付与します。}
    {parameter start, 開始番号}
    {parameter step, 増加数}
    {details
        {curl-code
            || AUTO_INCREASE
            {RecordSet
                {RecordFields
                    {RecordField
                        "col1", domain = int,
                        default-value = {RecordFieldUtil.auto-increment}
                    },
                    {RecordField
                        "col2", domain = String
                    }
                }
            }
        }
    }
    {notes 通常は{docref RecordField}のdefault-value引数に指定します。}
  }
  {define-proc public inline {auto-increment
                                 start:int64 = 1,
                                 step:int64 = 1
                             }:{proc-type {RecordField, RecordSet}:any}
    || TODO: unique key check (?)
    {return
        {fn rf, rs =>
            def name = rf.name
            def v =
                {RecordView
                    rs, sort = {RecordSort
                                   {fn r1, r2 =>
                                       {if r1[name] < r2[name] then
                                           1
                                        elseif r1[name] == r2[name] then
                                           0
                                        else
                                           -1
                                       }
                                   }
                               }
                }
            {if v.size > 0 then
                v[0][name] + step
             else
                start
            }
        }
    }
  }
}
